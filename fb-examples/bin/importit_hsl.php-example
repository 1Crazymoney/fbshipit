<?hh
/**
 * Copyright (c) 2017-present, Facebook, Inc.
 * All rights reserved.
 *
 * This source code is licensed under the BSD-style license found in the
 * LICENSE file in the root directory of this source tree. An additional grant
 * of patent rights can be found in the PATENTS file in the same directory.
 */
namespace Facebook\ImportIt;

require_once(__DIR__.'/../autoload.php');

use type Facebook\ShipIt\{
  ShipItChangeset,
  ShipItConditionalLinesFilter,
  ShipItHSL,
  ShipItPathFilters
};

final class ImportItHSL extends FBImportItCLI {
  const type TShipIt = ShipItHSL;

  <<__Override>>
  protected static function projectFilterChangeset(
    ShipItChangeset $changeset,
  ): ShipItChangeset {
    return $changeset
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> preg_replace(
          '@^tests/(.+/)?@',
          'src/\1__tests__/',
          $path,
        ),
      )
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> preg_replace(
          '@^bin/(.+)\\.php$@',
          'bin/\\1_dot_php',
          $path,
        ),
      )
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> preg_replace('/^\\./', 'dot_', $path),
      )
      |> ShipItConditionalLinesFilter::uncommentLines(
        $$,
        '@oss-disable',
        '//',
      )
      |> ShipItConditionalLinesFilter::commentLines(
        $$,
        '@oss-enable',
        '//',
      )
      |> ShipItHSL::renameAsyncToGen($$)
      |> parent::projectFilterChangeset($$);
  }
}

// Allow require() from unit test
if (isset($argv) && realpath($argv[0]) === realpath(__FILE__)) {
  ImportItHSL::cliMain();
}
