<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */
namespace Facebook\ShipIt\Config;

use type Facebook\ShipIt\{
  FBShipItCLIStaticConfig,
  FBShipItConfigeratorConfig,
  FBSourceBranchConfig,
  GetPathMappingsFn,
  ShipItBaseConfig,
  ShipItChangeset,
};

final class FacebookItCpePlugin extends FBShipItConfigeratorConfig {
  const string BASE_DIR = 'chef/itchef/cookbooks/core/';

  # Add new open-source cookbooks here. Be sure to get sign-off
  # from the OpenSource team.
  static ImmSet<string> $cookbooks = ImmSet {
    'cpe_helpers',
    'cpe_hosts',
    'cpe_profiles',
  };

  public static function getDirs(): ImmSet<string> {
    return self::$cookbooks->map($cookbook ==> self::BASE_DIR.$cookbook.'/')
      ->concat(vec[
        'chef/itchef/github/',
        'chef/itchef/github-deprecated/',
        ])
      ->toImmSet();
  }

  public static function stripChefTestHostsHeader(
    ShipItChangeset $changeset,
  ): ShipItChangeset {
    return $changeset->withMessage(
      /* HH_IGNORE_ERROR[2049] __PHPStdLib */
      /* HH_IGNORE_ERROR[4107] __PHPStdLib */
      \preg_replace('/\s*chef_test_hosts:.*/', '', $changeset->getMessage()),
    );
  }

  <<__Override>>
  public function defaultProjectFilterChangeset(
    ShipItChangeset $changeset,
    GetPathMappingsFn $get_path_mappings,
  ): ShipItChangeset {
    return parent::defaultProjectFilterChangeset($changeset, $get_path_mappings)
      |> static::stripChefTestHostsHeader($$);
  }

  <<__Override>>
  public function getBaseConfig(
    FBSourceBranchConfig $branch_config,
  ): ShipItBaseConfig {
    return parent::getBaseConfig($branch_config)
      ->withSourceRoots(self::getDirs());
  }
}
