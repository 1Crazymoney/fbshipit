<?hh // strict
/**
 * Copyright (c) 2016-present, Facebook, Inc.
 * All rights reserved.
 *
 * This source code is licensed under the BSD-style license found in the
 * LICENSE file in the root directory of this source tree. An additional grant
 * of patent rights can be found in the PATENTS file in the same directory.
 */
namespace Facebook\ShipIt\Config;

use \Facebook\ShipIt\ {
  FBShipItCLIStaticConfig,
  FBShipItConfig,
  FBSourceBranchConfig,
  HHVMBuildAndTestPhase,
  HHVMCleanThirdPartyPhase,
  HHVMEnvironmentCheckPhase,
  ShipItFilterSanityCheckPhase,
  ShipItGitHubInitPhase,
  ShipItPhase,
  ShipItVerifyRepoPhase,
};

final class FacebookHhvm extends FBShipItConfig {

  <<__Override>>
  public static function getDefaultPathMappings(): ImmMap<string, string> {
    return ImmMap {
      'fbcode/hphp/public_tld/' => '',
      'fbcode/hphp/' => 'hphp/',
    };
  }

  <<__Override>>
  public static function getSubmoduleMappings(): ImmMap<string, string> {
    return ImmMap {
      'fbcode/hphp/facebook/third-party-rev.txt' => 'third-party',
    };
  }

  <<__Override>>
  protected static function getStripCommonFilesExceptions(): ImmVector<string> {
    return ImmVector {
      // HHVM has an extension named "fb" which is public
      '@^fbcode/hphp/runtime/ext/fb/@',
    };
  }

  <<__Override>>
  protected static function getDefaultStrippedFiles(): ImmVector<string> {
    return ImmVector {
      '@^(?!fbcode/hphp)@',
      '@^fbcode/hphp/(?!(hack|doc)).*Makefile$@',
    };
  }

  <<__Override>>
  protected static function getDefaultStrippedFileExceptions(
  ): ImmVector<string> {
    // Do not strip any submodules that have already been written out.
    return self::getSubmoduleMappings()
      ->values()
      ->map($path ==> '@^'.$path.'$@')
      // Nor the test file used by the sanity check filter.
      ->toVector()
      ->add('@.*'.ShipItFilterSanityCheckPhase::TEST_FILE_NAME.'$@')
      ->toImmVector();
  }

  <<__Override>>
  public static function getPhases(
    FBSourceBranchConfig $branch_config,
  ): ImmVector<ShipItPhase> {
    $parentPhases = parent::getPhases($branch_config)->toVector();

    $phases = Vector { new HHVMEnvironmentCheckPhase() };

    foreach ($parentPhases as $phase) {
      $phases->add($phase);
      if ($phase instanceof ShipItGitHubInitPhase) {
        $phases->add(new HHVMCleanThirdPartyPhase());
      } elseif ($phase instanceof ShipItVerifyRepoPhase) {
        $phases->add(new HHVMBuildAndTestPhase());
      }
    }

    return $phases->toImmVector();
  }

  <<__Override>>
  public static function getStaticConfig(): FBShipItCLIStaticConfig {
    return shape(
      'internalRepo' => 'fbsource',
      'githubOrg' => 'facebook',
      'githubProject' => 'hhvm',
    );
  }
}
