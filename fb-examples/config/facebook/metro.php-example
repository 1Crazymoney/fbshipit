<?hh // strict
/**
 * Copyright (c) 2016-present, Facebook, Inc.
 * All rights reserved.
 *
 * This source code is licensed under the BSD-style license found in the
 * LICENSE file in the root directory of this source tree. An additional grant
 * of patent rights can be found in the PATENTS file in the same directory.
 */
namespace Facebook\ShipIt\Config;

use \Facebook\ShipIt\ {
  FBCommonFilters,
  FBShipItCLIStaticConfig,
  FBShipItConfig,
  FBSourceBranchConfig,
  GetPathMappingsFn,
  ShipItChangeset,
  ShipItPathFilters,
};

final class FacebookMetro extends FBShipItConfig {
  const string ROOT = 'xplat/js/metro/';

  <<__Override>>
  protected static function defaultProjectFilterChangeset(
    ShipItChangeset $changeset,
    GetPathMappingsFn $get_path_mappings,
  ): ShipItChangeset {
    return $changeset
      |> FBCommonFilters::stripSummariesByDefault($$)
      |> FBCommonFilters::applyAll(
          $$,
          shape(
            // FBCommonFilters defines a common set of files to ignore
            // across all fb projects, such as BUCK files and others.
            // We want to export *all* of the react-native-github folder
            // to GitHub, except for a few specific paths.
            'stripCommonFiles/exceptions' => ImmVector {
              '/.*/',
            },
          ),
        )
      |> ShipItPathFilters::rewritePaths(
          $$,
          $path ==> \preg_replace(
            '@/\.github\.flowconfig$@',
            '/.flowconfig',
            $path,
          ),
        )
      |> ShipItPathFilters::moveDirectories($$, $get_path_mappings());
  }

  <<__Override>>
  protected static function projectImportChangeset(
    ShipItChangeset $changeset,
    FBSourceBranchConfig $branch_config,
  ): ShipItChangeset {
    return $changeset
      |> \Facebook\ShipIt\ShipItPathFilters::rewritePaths(
          $$,
          $path ==> \preg_replace(
            '@(^|/)\.flowconfig$@',
            '\1.github.flowconfig',
            $path,
          ),
        )
      |> parent::projectImportChangeset(
        $$,
        $branch_config,
      );
  }

  <<__Override>>
  public static function getDefaultPathMappings(): ImmMap<string, string> {
    return ImmMap {
      self::ROOT => '',
    };
  }

  <<__Override>>
  public static function getStaticConfig(): FBShipItCLIStaticConfig {
    return shape(
      'internalRepo' => 'fbsource',
      'githubOrg' => 'facebook',
      'githubProject' => 'metro',
    );
  }
}
