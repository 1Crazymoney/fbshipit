<?hh // strict
/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

/**
 * This file was moved from fbsource to www. View old history in diffusion:
 * https://fburl.com/gvcpz1nl
 */
namespace Facebook\ShipIt\Config;

use namespace HH\Lib\Regex;
use type Facebook\ShipIt\{
  FBShipItCLIStaticConfig,
  FBShipItConfig,
  FBSourceBranchConfig,
  GetPathMappingsFn,
  ShipItChangeset,
  ShipItPathFilters,
};

final class FacebookReactNative extends FBShipItConfig {
  const string ROOT = 'xplat/js/react-native-github/';

  <<__Override>>
  public function forceConfigeratorGenerationWithHackPlugin(): string {
    return FacebookReactNativePlugin::class;
  }

  <<__Override>>
  public function defaultProjectFilterChangeset(
    ShipItChangeset $changeset,
    GetPathMappingsFn $get_path_mappings,
  ): ShipItChangeset {
    return $changeset
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==>
          Regex\replace($path, re"@/\.github\.flowconfig$@", '/.flowconfig'),
      )
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> Regex\replace(
          $path,
          re"@/\.github\.flowconfig.android$@",
          '/.flowconfig.android',
        ),
      )
      |> parent::defaultProjectFilterChangeset($$, $get_path_mappings);
  }

  <<__Override>>
  public function getStripCommonFileExceptions(): ImmVector<string> {
    // FBCommonFilters defines a common set of files to ignore
    // across all fb projects, such as BUCK files and others.
    // We want to export *all* of the react-native-github folder
    // to GitHub, except for a few specific paths
    // (see getDefaultStrippedFiles()).
    return ImmVector {
      '/.*/',
    };
  }

  <<__Override>>
  public function getDefaultStrippedFiles(): ImmVector<string> {
    return ImmVector {
      '@^'.self::ROOT.'BUCK$@',
      '@(^|/)node_modules/@',
      '@(^|/)npm-shrinkwrap.json@',
      '@(^|/)packager/react-packager/src/packager_experimental/@',
      '@(^|/)Libraries/Renderer/fb/@',
      '@(^|/)RNTester/Pods/@',
    };
  }

  <<__Override>>
  public function getDefaultPathMappings(): ImmMap<string, string> {
    return ImmMap {
      self::ROOT => '',
    };
  }

  <<__Override>>
  public function getStaticConfig(): FBShipItCLIStaticConfig {
    return shape(
      'internalRepo' => 'fbsource',
      'githubOrg' => 'facebook',
      'githubProject' => 'react-native',
    );
  }

  <<__Override>>
  public function projectImportChangeset(
    ShipItChangeset $changeset,
    FBSourceBranchConfig $branch_config,
  ): ShipItChangeset {
    return $changeset
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> Regex\replace(
          $path,
          re"@(^|/)\.flowconfig$@",
          '\1.github.flowconfig',
        ),
      )
      |> ShipItPathFilters::rewritePaths(
        $$,
        $path ==> Regex\replace(
          $path,
          re"@(^|/)\.flowconfig.android$@",
          '\1.github.flowconfig.android',
        ),
      )
      |> parent::projectImportChangeset($$, $branch_config);
  }
}
