<?hh // strict
/**
 * Copyright (c) 2016-present, Facebook, Inc.
 * All rights reserved.
 *
 * This source code is licensed under the BSD-style license found in the
 * LICENSE file in the root directory of this source tree. An additional grant
 * of patent rights can be found in the PATENTS file in the same directory.
 */
namespace Facebook\ShipIt;

final class HHVMCleanThirdPartyPhase extends ShipItPhase {
  <<__Override>>
  protected function isProjectSpecific(): bool {
    return true;
  }

  <<__Override>>
  final public function getReadableName(): string {
    return 'Clean hhvm/third-party/';
  }

  <<__Override>>
  final public function getCLIArguments(): ImmVector<ShipItCLIArgument> {
    return ImmVector {
      shape(
        'long_name' => 'skip-clean-third-party',
        'description' => "Don't ".$this->getReadableName(),
        'write' => $_ ==> $this->skip(),
      ),
    };
  }
  <<__Override>>
  final protected function runImpl(ShipItBaseConfig $config): void {
    $root = $config->getDestinationPath();
/* HH_FIXME[4128] Use ShipItShellCommand */
    ShipItUtil::shellExec(
      $root,
      /* stdin = */ null,
      ShipItUtil::DONT_VERBOSE,
      'rm', '-rf', 'third-party',
    );
/* HH_FIXME[4128] Use ShipItShellCommand */
    ShipItUtil::shellExec(
      $root,
      /* stdin = */ null,
      ShipItUtil::VERBOSE_SHELL_OUTPUT,
      'git', 'submodule', 'update', '--init', '--recursive',
    );
  }
}
